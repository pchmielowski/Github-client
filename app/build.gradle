apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "net.chmielowski.github"
        minSdkVersion 26
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            //noinspection GrDeprecatedAPIUsage
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.2'
    //noinspection GradleDependency - there is a problem with 1.0.2
    implementation 'com.android.support.constraint:constraint-layout:1.0.0'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    testImplementation "org.robolectric:robolectric:3.5.1"

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    implementation 'com.annimon:stream:1.1.9'
    final DAGGER_VERSION = '2.9'
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    androidTestCompile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    final MOCKITO_VERSION = '2.7.22'
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestCompile "org.mockito:mockito-android:$MOCKITO_VERSION"
    final LOMBOK_VERSION = "1.16.18"
    implementation "org.projectlombok:lombok:$LOMBOK_VERSION"
    annotationProcessor "org.projectlombok:lombok:$LOMBOK_VERSION"

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.0.0'

    //noinspection GradleDependency
    implementation 'com.facebook.stetho:stetho:1.4.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.uphyca:stetho_realm:2.0.0'

    final def LEAK_CANARY_VERSION = '1.5.1'
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.google.android.gms:play-services-gcm:11.8.0'

    implementation 'com.jakewharton.timber:timber:4.6.0'
    compile project(path: ':networkstate')
}

// Ensure the no-op dependency is always used in JVM tests.
configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}